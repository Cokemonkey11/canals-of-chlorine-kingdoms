package Bounty

import DamageEvent
import HashMap
import SoundUtils
import StandardTextTags

import Barracks
import ChooseHero
import Creeps
import Teams
import UnitIds


constant bounties = new HashMap<int, int>()
..put(ID_FOOTMAN, 1)
..put(ID_ARCHER, 1)
..put(ID_MORTAR_TEAM, 1)
..put(UnitIds.spiritwolf1, 1)
..put(UnitIds.spiritwolf2, 1)
..put(UnitIds.spiritwolf3, 1)

constant HERO_BOUNTY = 10

constant TOWER_BOUNTY = 10

constant UNIT_EXPERIENCE_BOUNTY = 24 // People were happy at 16 but I would like players to easily get level 6 before end of game

constant BOUNTY_DIST = 1500.

constant TOWER_DESTROYED_WARNING_SOUND = new SoundDefinition(Sounds.warning)


function doesDamageKillUnit(real hp, real damage) returns bool
    // https://www.hiveworkshop.com/threads/real-talk-floats-in-warcraft-3.270579/
    let minAliveHp = 0.404998779
    if hp - damage < minAliveHp
        return true

    return false


init
    DamageEvent.addListener(1) ->
        let damage = DamageEvent.getAmount()
        let dying_unit = DamageEvent.getTarget()
        let killing_player = DamageEvent.getSource().getOwner()
        let dying_unit_type = dying_unit.getTypeId()
        let dying_player = dying_unit.getOwner()

        if doesDamageKillUnit(dying_unit.getHP(), damage)
            // If the unit gives bounty, give its killer the bounty.
            if bounties.has(dying_unit_type)
                if dying_player.isEnemyOf(killing_player)
                    for hr in all_heroes
                        if hr.isEnemyOf(dying_player) and hr.isAlive() and hr.getPos().distanceToSq(dying_unit.getPos()) < BOUNTY_DIST * BOUNTY_DIST
                            hr.addXp(UNIT_EXPERIENCE_BOUNTY, true)

                    if player_heroes.has(killing_player)
                        killing_player.addGold(bounties.get(dying_unit_type))
                        createGoldBountyTextTag(dying_unit, bounties.get(dying_unit_type), killing_player)

                else if player_heroes.has(killing_player)
                    standardTextTag(dying_unit.getPos(), "!")

            if dying_unit.isType(UNIT_TYPE_HERO)
                if dying_player.isEnemyOf(killing_player)
                    killing_player.addGold(HERO_BOUNTY)
                    createGoldBountyTextTag(dying_unit, HERO_BOUNTY, killing_player)

            if dying_unit_type == ID_CANNON_TOWER
                siegeQueue.add(ID_MORTAR_TEAM)
                for pl in dying_player.getForce().opposingForce()
                    createGoldBountyTextTag(dying_unit, TOWER_BOUNTY, pl)
                    pl.addGold(TOWER_BOUNTY)
                for pl in dying_player.getForce()
                    printTimedToPlayer("Your tower was destroyed.", 30., pl)
                    TOWER_DESTROYED_WARNING_SOUND.playForPlayer(pl)


@test function testKillUnit()
    assertTrue(doesDamageKillUnit(1, 1))

    assertTrue(not doesDamageKillUnit(1.5, 1))

    assertTrue(doesDamageKillUnit(1.404, 1))
