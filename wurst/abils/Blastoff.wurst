package Blastoff

import ObjectIdGenerator
import ClosureEvents
import ClosureForGroups
import ClosureTimers
import Knockback3
import LinkedListModule
import SoundUtils

import CodeDamage


constant BLASTOFF_SPEED = 1300.
public constant BLASTOFF_AREA_OF_EFFECT = 325.
public constant ID_BLASTOFF = compiletime(ABIL_ID_GEN.next())
constant GO_SOUND = new SoundDefinition(Sounds.flameStrikeBirth1)


class Blastoff
    use LinkedListModule

    vec2 vel
    unit caster
    real distLeft

    static function damageFn(int level) returns real
        return 100. + 50 * level

    construct(unit caster, vec2 target)
        this.caster = caster
        this.vel = ZERO2.polarOffset(caster.getPos().angleTo(target), BLASTOFF_SPEED * ANIMATION_PERIOD)
        this.distLeft = caster.getPos().distanceTo(target)

        GO_SOUND.playOnPoint(this.caster.getPos3Real()).snd.setVolume(127)

        if size == 1
            doPeriodically(ANIMATION_PERIOD) cb ->
                for blastoff from staticItr()
                    blastoff.caster.setXY(blastoff.caster.getPos() + blastoff.vel)
                    blastoff.distLeft -= (BLASTOFF_SPEED * ANIMATION_PERIOD)

                    if blastoff.distLeft <= 0.
                        forUnitsInRange(blastoff.caster.getPos(), BLASTOFF_AREA_OF_EFFECT) u ->
                            if u.isEnemyOf(blastoff.caster) and u.isAlive() and not u.isInvulnerable()
                                blastoff.caster.dealCodeDamage(u, damageFn(blastoff.caster.getAbilityLevel(ID_BLASTOFF)))
                                Knockback3.setVel(u, 700., angle(0.), angle(0.))

                    if blastoff.distLeft <= 0. or not blastoff.caster.isAlive()
                        destroy blastoff

                if size == 0
                    destroy cb


init
    EventListener.add(EVENT_PLAYER_UNIT_SPELL_EFFECT) ->
        if EventData.getSpellAbilityId() == ID_BLASTOFF
            let caster = EventData.getTriggerUnit()
            let target = EventData.getAbilityTargetPos()
            new Blastoff(caster, target)
