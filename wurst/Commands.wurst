package Commands

import AbilityIds
import ClosureForGroups
import LinkedList
import OrderIds
import UnitIds

import WurstCommand

import Bind
import Castles
import CentaurWorldbender
import Events
import Flash
import GameBoard
import GlobalCooldownModifiers
import GoblinBloodbomber
import PowerLevelingChamber
import Stun
import Teams


init
    addCommand("name") (p, _cmd, args) ->
        let name = args.joinBy(" ")
        player_heroes.get(p).setProperName(name)
        p.updateBoardName(name.color(p.getColor().toColor()))

    addCommand("repick") (p, _cmd, _args) ->
        p.print("You can't.".color(COLOR_GOLD))

    if isSinglePlayer()
        addCommand("level") (p, _cmd, args) ->
            forUnitsOfPlayer(p) (unit u) ->
                if u.isType(UNIT_TYPE_HERO)
                    let amt = args.size() > 0 and args.get(0).toInt() > 0 ? args.get(0).toInt() : u.getLevel() + 1
                    u.setLevel(amt, true)

        addCommand("stun") (p, _cmd, args) ->
            forUnitsOfPlayer(p) (unit u) ->
                if u.isType(UNIT_TYPE_HERO)
                    let amt = args.size() > 0 and args.get(0).toInt() > 0 ? args.get(0).toInt() : 10
                    u.stun(amt.toReal())

        addCommand("stormbolt") (p, _cmd, args) ->
            forUnitsOfPlayer(p) (unit u) ->
                if u.isType(UNIT_TYPE_HERO)
                    createUnit(players[1], UnitIds.mountainking, u.getPos().moveTowards(ZERO2, 500.))
                        ..removeAbility(AbilityIds.attack)
                        ..addAbility(AbilityIds.stormBolt)
                        ..setAbilityLevel(AbilityIds.stormBolt, 2)
                        ..issueTargetOrder("thunderbolt", u)

        addCommand("flash") (p, _cmd, _args) ->
            forUnitsOfPlayer(p) (unit u) ->
                if u.isType(UNIT_TYPE_HERO)
                    u..resetAbilityCooldown(ID_FLASH)

        addCommand("bind") (p, _cmd, _args) ->
            forUnitsOfPlayer(p) (unit u) ->
                if u.isType(UNIT_TYPE_HERO)
                    u..bind(4.)

        addCommand("win") (p, _cmd, _args) ->
            castles[1].kill()

        addCommand("dump") (p, _cmd, _args) ->
            p.eventDump()

        addCommand("enemy") (p, _cmd, _args) ->
            createUnit(players[1], UnitIds.darkranger)

        addCommand("goblin") (p, _cmd, _args) ->
            createUnit(players[2], ID_GOBLIN_BLOODBOMBER)

        addCommand("centaur") (p, _cmd, _args) ->
            createUnit(players[2], ID_CENTAUR_WORLDBENDER)

        addCommand("generator") (p, _cmd, _args) ->
            enableGenerator()

        addCommand("cooldown") (p, _cmd, _args) ->
            setGlobalCooldownModifier(0.01)
