package Commands

import ClosureForGroups
import LinkedList
import UnitIds

import WurstCommand

import Bind
import Castles
import ChooseHero
import Events
import Flash
import GameBoard
import PowerLevelingChamber
import Teams


init
    addCommand("name") (p, _cmd, args) ->
        let name = args.joinBy(" ")
        player_heroes.get(p).setProperName(name)
        p.updateBoardName(name.color(p.getColor().toColor()))

    addCommand("repick") (p, _cmd, _args) ->
        p.print("You can't.".color(COLOR_GOLD))

    if isSinglePlayer()
        addCommand("level") (p, _cmd, _args) ->
            forUnitsOfPlayer(p) (unit u) ->
                if u.isType(UNIT_TYPE_HERO)
                    u.setLevel(u.getLevel() + 1, true)

        addCommand("flash") (p, _cmd, _args) ->
            forUnitsOfPlayer(p) (unit u) ->
                if u.isType(UNIT_TYPE_HERO)
                    u..resetAbilityCooldown(ID_FLASH)

        addCommand("bind") (p, _cmd, _args) ->
            forUnitsOfPlayer(p) (unit u) ->
                if u.isType(UNIT_TYPE_HERO)
                    u..bind(4.)

        addCommand("win") (p, _cmd, _args) ->
            castles[1].kill()

        addCommand("dump") (p, _cmd, _args) ->
            p.eventDump()

        addCommand("enemy") (p, _cmd, _args) ->
            createUnit(players[1], UnitIds.paladin)

        addCommand("generator") (p, _cmd, _args) ->
            enableGenerator()
