package SpaceCadet

// - executor 5, 10, 15, 20
// - berserk 35, 55, 75, 95

import Assets

import AbilityTooltipGenerator

import HeroGlow


public constant ID_SPACE_CADET = compiletime(HERO_ID_GEN.next())

public constant ID_DIVINE_SHIELD = compiletime(ABIL_ID_GEN.next())

public constant ID_FLASH = compiletime(ABIL_ID_GEN.next())

public constant FLASH_DISTANCE = 400.


@compiletime function genSpaceCadet()
    let tgen = new AbilityTooltipGenerator("Grants invulnerability for a short duration.")
    new AbilityDefinitionPaladinDivineShield(ID_DIVINE_SHIELD)
    ..setButtonPositionNormalX(3)
    ..setButtonPositionNormalY(2)
    ..setButtonPositionResearchX(3)
    ..registerTooltipGenerator(tgen)
    ..presetHotkey("R")
    ..setRequiredLevel(6)
    ..setLevelSkipRequirement(2)

    ..tooltipStartListen()
    ..setLevels(3)
    ..presetDurationHero((int lvl) -> 3.0 + 2.0 * (lvl - 1.0))
    ..setName("Divine Shield")
    ..presetCooldown((int lvl) -> 60. - (10 * (lvl - 1)))
    ..tooltipStopListen(true)

    ..presetCanDeactivate(int lvl -> false)
    ..presetManaCost(int lvl -> 0)

    let tgen2 = new AbilityTooltipGenerator("Blinks a short distance in the direction of the " + "last".color(color(170, 0, 0)) + " |cff919191right-click.|r")
    new AbilityDefinitionWindWalk(ID_FLASH)
    ..registerTooltipGenerator(tgen2)
    ..setIconNormal(Icons.bTNBearBlink)
    ..setIconResearch(Icons.bTNBearBlink)

    ..tooltipStartListen()
    ..setLevels(3)
    ..setName("Flash")
    ..presetCooldown((int lvl) -> 60. - (15 * (lvl - 1)))
    ..addTooltipProperty("Range", (int lvl) -> FLASH_DISTANCE)
    ..tooltipStopListen(true)

    ..presetBackstabDamage(lvl -> false)
    ..presetMovementSpeedIncrease(lvl -> 0.)
    ..presetDurationHero(lvl -> 0.1)
    ..presetTransitionTime(lvl -> 0.)
    ..presetManaCost(int lvl -> 50)
    ..setButtonPositionNormalX(2)
    ..setButtonPositionNormalY(2)
    ..setButtonPositionResearchX(2)
    ..presetHotkey("E")

    new HeroDefinition(ID_SPACE_CADET, UnitIds.darkranger)
    ..setName("Space Cadet")
    ..setProperNames(commaList("Richie", "Jared", "Felix"))
    ..setProperNamesUsed(3)
    ..setAttack1DamageBase(34)
    ..setAttack1DamageNumberofDice(1)
    ..setAttack1DamageSidesperDie(1)
    ..setAttack1WeaponType(WeaponType.Instant)
    ..setAttack1ProjectileArt(Abilities.rifleImpact)
    ..setModelFile(Units.marine)
    ..setNormalAbilities(commaList(AbilityIds.inventory, ID_HERO_GLOW))
    ..setAttack1AnimationBackswingPoint(0.35)
    ..setAttack1AnimationDamagePoint(.1)
    ..setUnitSoundSet("Marine")
    ..setHeroAbilities(commaList(AbilityIds.stormBolt, AbilityIds.enduranceAura1, ID_FLASH, ID_DIVINE_SHIELD))
    ..setIconGameInterface(Icons.bTNMarine)
