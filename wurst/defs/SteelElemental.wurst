package SteelElemental

import Assets

import AbilityTooltipGenerator

import Blastoff
import Clap
import HeroGlow
import WarStomp


public constant ID_STEEL_ELEMENTAL = compiletime(HERO_ID_GEN.next())

public constant ID_ARMOR_UP = compiletime(ABIL_ID_GEN.next())


@compiletime function genSteelElemental()
    let tgen = new AbilityTooltipGenerator("Dashes to the target area, knocking up enemies upon arrival.")
    new ChannelAbilityPreset(ID_BLASTOFF, 3, true, tgen)
    ..setHeroAbility(true)
    ..setLevelSkipRequirement(2)
    ..setRequiredLevel(6)

    ..tooltipStartListen()
    ..presetTargetTypes(Targettype.POINT)
    ..presetManaCost(_ -> 90)
    ..presetCastRange((int lvl) -> 700. + 100 * lvl)
    ..setName("Blastoff")
    ..presetCooldown(lvl -> 120. - (10. * lvl))
    ..addTooltipProperty("Damage", lvl -> 100. + 50. * lvl)
    ..addTooltipProperty("Area of Effect", lvl -> BLASTOFF_AREA_OF_EFFECT)
    ..tooltipStopListen()

    ..presetButtonPosNormal(3, 2)
    ..presetButtonPosResearch(3, 0)
    ..presetHotkey("R")
    ..presetIcon(Icons.bTNClusterRockets)

    let tgen2 = new AbilityTooltipGenerator("Provides a temporary armor boost.")
    new AbilityDefinitionBerserk(ID_ARMOR_UP)
    ..registerTooltipGenerator(tgen2)
    ..setHeroAbility(true)

    ..tooltipStartListen()
    ..setLevels(3)
    ..presetManaCost(_ -> 60)
    ..setName("Armor Up")
    ..presetCooldown(lvl -> 20.)
    ..addTooltipProperty("Duration", lvl -> 10.)
    ..addTooltipProperty("Armor Bonus", lvl -> 10 * lvl)
    ..tooltipStopListen()

    ..presetButtonPosNormal(1, 2)
    ..presetButtonPosResearch(1, 0)
    ..presetHotkey("W")
    ..presetIcon(Icons.bTNArmorGolem)
    ..presetDurationHero((int lvl) -> 0.1)
    ..setLevelSkipRequirement(2)

    new HeroDefinition(ID_STEEL_ELEMENTAL, UnitIds.taurenchieftain)
    ..setName("Steel Elemental")
    ..setProperNames(commaList("Sharpy", "Pointy", "Rough Edges"))
    ..setProperNamesUsed(3)
    ..setAttack1DamageBase(34)
    ..setAttack1DamageNumberofDice(1)
    ..setAttack1DamageSidesperDie(1)
    ..setAttack1WeaponType(WeaponType.Normal)
    ..setModelFile(Units.golemStatue1)
    ..setNormalAbilities(commaList(AbilityIds.inventory, ID_HERO_GLOW))
    ..setUnitSoundSet(Soundsets.rockGolem)
    ..setHeroAbilities(commaList(ID_CLAP, ID_ARMOR_UP, ID_WAR_STOMP, ID_BLASTOFF))
    ..setIconGameInterface(Icons.bTNArmorGolem)
