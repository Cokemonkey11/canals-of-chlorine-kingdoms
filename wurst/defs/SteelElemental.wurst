package SteelElemental

// Steel elemental (granite elemental)
// - Thunderclap 1, 10, 100, 1000
// - Armor up 10, 20, 30, 40
// - Haunt (nocturne)
// - power thrust (malphite)

import Assets

import AbilityTooltipGenerator

import Blastoff
import ClassicHeroes
import HeroGlow


constant x = IMPORT_ORDER_DUMMY

public constant ID_STEEL_ELEMENTAL = compiletime(HERO_ID_GEN.next())

public constant ID_ARMOR_UP = compiletime(ABIL_ID_GEN.next())


@compiletime function genSteelElemental()
    let tgen = new AbilityTooltipGenerator("Dashes to the target area, knocking up enemies upon arrival.")
    new ChannelAbilityPreset(ID_BLASTOFF, 3, true, tgen)
    ..setHeroAbility(true)
    ..presetButtonPosNormal(3, 2)
    ..presetButtonPosResearch(3, 0)
    ..presetHotkey("R")
    ..presetIcon(Icons.bTNClusterRockets)
    ..setLevelSkipRequirement(2)
    ..setRequiredLevel(6)

    ..tooltipStartListen()
    ..presetTargetTypes(Targettype.POINT)
    ..presetManaCost(_ -> 90)
    ..presetCastRange((int lvl) -> 700. + 100 * lvl)
    ..setName("Blastoff")
    ..presetCooldown(lvl -> 120. - (10. * lvl))
    ..addTooltipProperty("Damage", lvl -> 100. + 50. * lvl)
    ..addTooltipProperty("Area of Effect", lvl -> BLASTOFF_AREA_OF_EFFECT)
    ..tooltipStopListen()

    let tgen2 = new AbilityTooltipGenerator("Provides a temporary armor boost.")
    new AbilityDefinitionBerserk(ID_ARMOR_UP)
    ..registerTooltipGenerator(tgen2)
    ..setHeroAbility(true)
    ..presetButtonPosNormal(1, 2)
    ..presetButtonPosResearch(1, 0)
    ..presetHotkey("W")
    ..presetIcon(Icons.bTNArmorGolem)
    ..presetDurationHero((int lvl) -> 0.1)

    ..tooltipStartListen()
    ..setLevels(3)
    ..presetManaCost(_ -> 60)
    ..setName("Armor Up")
    ..presetCooldown(lvl -> 20.)
    ..addTooltipProperty("Duration", lvl -> 10.)
    ..addTooltipProperty("Armor Bonus", lvl -> 10 * lvl)
    ..tooltipStopListen()

    new HeroDefinition(ID_STEEL_ELEMENTAL, UnitIds.taurenchieftain)
    ..setName("Steel Elemental")
    ..setProperNames(commaList("Sharpy", "Pointy", "Rough Edges"))
    ..setProperNamesUsed(3)
    ..setAttack1DamageBase(34)
    ..setAttack1DamageNumberofDice(1)
    ..setAttack1DamageSidesperDie(1)
    ..setAttack1WeaponType(WeaponType.Normal)
    ..setModelFile(Units.golemStatue1)
    ..setNormalAbilities(commaList(AbilityIds.inventory, ID_HERO_GLOW))
    ..setUnitSoundSet(Soundsets.rockGolem)
    ..setHeroAbilities(commaList(AbilityIds.thunderClap, ID_ARMOR_UP, AbilityIds.warStomp1, ID_BLASTOFF))
    ..setIconGameInterface(Icons.bTNArmorGolem)
